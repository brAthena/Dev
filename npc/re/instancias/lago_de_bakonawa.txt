//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: lago_de_bakonawa.txt                   |
//   |--------------------------------------------------------|
//   | Criado por: Herc/brA Dev. Team                         |
//   |--------------------------------------------------------|
//   | Versões Herc/brA: 1.0/1.0                              |
//   |--------------------------------------------------------|
//   | Descrição: Lago de Bakonawa                            |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 First version. [Euphy]                             |
//   | 1.1 Added GM management NPC. [Euphy]                   |
//   | 1.2 Cabeçalho brAthena e tradução. [SoulKnown]         |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

1@ma_b	mapflag	src4instance

ma_scene01,174,179,4	script	Taho	4_M_DEWZATIMAN,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "Você está carregando muitos itens. Retorne quando estiver com menos objetos.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "Você está sobrecarregado. Volte quando estiver mais leve.";
		close;
	}
	if (BaseLevel < 140) {
		mes "[Taho]";
		mes "Ei. Este lugar é muito perigoso para você. Você não pode se manter seguro aqui. Volte agora mesmo para a aldeia.";
		close;
	}
	if (malaya_bakona1 < 7 && malaya_bakona2 < 15) {
		mes "[Taho]";
		mes "Nós seremos mortos pelo Bakonawa. Ah, não há o que temer, devemos matá-lo, mas, ainda não podemos.";
		close;
	}

	.@party_id = getcharid(1);
	.@md_name$ = "Lago de Bakonawa";

	.@baku_time = questprogress(12278,PLAYTIME);
	if (!.@baku_time) {
		if (!questprogress(12279)) setquest 12279;
	}
	if (!.@party_id) {
		mes "[Taho]";
		mes "Não há nenhuma companhia que possa vir com você? Ou, mesmo que você queira ir sozinho, pelo menos crie um grupo.";
		close;
	}
	if (!.@baku_time) {
		if (getcharid(0) == getpartyleader(.@party_id,2)) {
			mes "[Taho]";
			mes "Você é o líder dos guerreiros que matará Bakonawa? A estrada está passando por dificuldades estruturais, então temos que descer o penhasco com uma corda, está bem?";
			next;
			.@i = select("Por favor, faça uma corda.:Eu irei descer agora.:Cancelar.");
		} else {
			mes "[Taho]";
			mes "Você veio para matar o Bakonawa. O líder do seu grupo tem que me ajudar a tecer a corda para descer.";
			next;
			.@i = select(":Bakonawa Lake Agora vou descer.:Cancelar");
		}
		switch(.@i) {
		case 1:
			.@instance = instance_create(.@md_name$,.@party_id);
			if (.@instance < 0) {
				mes "[Taho]";
				mes "Oh, a corda se rompeu, preciso tecer uma nova.";
				close;
			}
			if (instance_attachmap("1@ma_b",.@instance) == "") {
				mes "^0000ff"+.@md_name$+"^000000 - a reserva falhou!";
				instance_destroy(.@instance);
				close;
			}
			instance_set_timeout 7200,300,.@instance;
			instance_init(.@instance);
			mes "[Taho]";
			mes "Bem, agora eu estou descendo, desça apenas eu terminar.";
			close;
		case 2:
			if( has_instance("1@ma_b") == "" ) {
				mes "O Memorial do Calabouço do "+.@md_name$+" não existe.";
				mes "O líder do grupo ainda não reservou o calabouço.";
				close;
			} else {
				mapannounce "ma_scene01", getpartyname(.@party_id)+" está entrando no calabouço Bakonawa Lake.",bc_map,"0x00ff99";
				setquest 12278;
				warp "1@ma_b",64,51;
				end;
			}
		case 3:
			close;
		}
	} else if (.@baku_time == 1) {
		mes "[Taho]";
		mes "A corda utilizada para chegar ao lago está quebrada. Estamos preparando as coisas novamente, então volte daqui a pouco.";
		close;
	} else if (.@baku_time == 2) {
		mes "[Taho]";
		mes "Está tudo limpo por aqui. Agora, você pode descer pela corda novamente.";
		erasequest 12278;
		close;
	}
}

sec_in02,26,26,4	script	Bakonawa	4_M_DEWZATIMAN,{
	callfunc "F_GM_NPC";
	mes "[Taho]";
	mes "Meu nome é Minky.";
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		erasequest 12278;
		erasequest 12279;
		malaya_bakona1 = 7;
		malaya_bakona2 = 15;
	}
	close;
}

1@ma_b,62,52,4	script	Taho#nf	4_M_DEWZATIMAN,{
	mes "[Taho]";
	mes "Heehee, eu poderia me juntar à caça de Bakonawa, mas prefiro ficar escondido atrás daquela pedra e dar apoio a vocês dali.";
	next;
	mes "[Taho]";
	mes "Bakonawa se tornará muito agressivo em algum momento. Quando chegar esse momento, ouça atentamente meus guias. Apenas confie em mim e tudo ficará bem.";
	next;
	if (getcharid(0) == getpartyleader(getcharid(1),2)) {
		mes "[Taho]";
		mes "Agora farei o Bakonawa sair do Lago.";
		next;
		switch(select("Espere! Eu não estou pronto ainda!:Pode prosseguir!")) {
		case 1:
			mes "[Taho]";
			mes "Oh... tudo bem... diga-me quando estiver pronto.";
			close;
		case 2:
			mes "[Taho]";
			mes "Pois bem, pingarei uma gota de Albopal neste lago.";
			next;
			mes "[Taho]";
			mes "Quando o Bakonawa sentir isso, ele irá surgir logo...";
			donpcevent instance_npcname("#Bakonawan1")+"::OnStart";
			disablenpc instance_npcname("Taho#nf");
			close;
		}
	} else {
		mes "[Taho]";
		mes "Estou pingando algumas gotas de Albopal para deixar o Bakonawa maluco. Reza lenda que o Bakonawa é uma ótima fonte de proteína.";
		close;
	}
}

1@ma_b,36,111,4	script	#Bakonawan1	CLEAR_NPC,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan1");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan1");
	.@map$ = instance_mapname("1@ma_b");
	mapannounce .@map$,"Taho: posso vê-lo na superficie! ATAQUE!!!",bc_map,"0x00ffff";
	monster .@map$,78,81,"Bakonawa",BAKONAWA_1,1,instance_npcname("#Bakonawan1")+"::OnMyMobDead"; //original: (78,83)
	initnpctimer;
	end;
OnFail:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan1")+"::OnMyMobDead";
	end;
OnMyMobDead:
	.@map$ = instance_mapname("1@ma_b");
	if (mobcount(.@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#BakonawaDeadn1")+"::OnStart";
		killmonster .@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan1");
	}
	end;
OnTimer1000:	callsub OnAnnounce,"10 minutos";
OnTimer60000:	callsub OnAnnounce,"9 minutos";
OnTimer120000:	callsub OnAnnounce,"8 minutos";
OnTimer180000:	callsub OnAnnounce,"7 minutos";
OnTimer240000:	callsub OnAnnounce,"6 minutos";
OnTimer300000:	callsub OnAnnounce,"5 minutos";
OnTimer360000:	callsub OnAnnounce,"4 minutos";
OnTimer420000:	callsub OnAnnounce,"3 minutos";
OnTimer480000:	callsub OnAnnounce,"2 minutos";
OnTimer540000:	callsub OnAnnounce,"1 minuto";
OnTimer570000:
	mapannounce instance_mapname("1@ma_b"),"Tempo Limite: 30 segundos.",bc_map,"0xff4400"; //FW_NORMAL 18 0 0
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
OnTimer600000:
	.@map$ = instance_mapname("1@ma_b");
	mapannounce .@map$,"Bakonawa escapou para o fundo do lago.",bc_map,"0xffff00";
	donpcevent instance_npcname("Taho#Fail")+"::OnStart";
	donpcevent instance_npcname("#Bakonawan1")+"::OnFail";
	killmonster .@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan1");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"Tempo Limite: "+getarg(0),bc_map,"0xff4400";
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,78,81,0	script	#SummonPuppetsn1	HIDDEN_WARP_NPC,4,5,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnStart:
	stopnpctimer;
	enablenpc instance_npcname("#SummonPuppetsn1");
	initnpctimer;
	end;
OnTouch:
	stopnpctimer;
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnTimer5000:
	setarray .@x[0],79,71,60,61,57,89,95,96,99;
	setarray .@y[0],71,72,80,90,99,73,82,90,99;
	.@map$ = instance_mapname("1@ma_b");
	for(.@i = 0; .@i<9; ++.@i) {
		.@rand = rand(1,10);
		if (.@rand > 7)
			monster .@map$,.@x[.@i],.@y[.@i],"Vontade de Bakonawa",HIDDEN_MOB,1,instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
		else if (.@rand < 4)
			monster .@map$,.@x[.@i],.@y[.@i],"Vontade de Bakonawa",HIDDEN_MOB_W,1,instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
	}
	end;
OnTimer50000:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnMyMobDead:
	end;
}

1@ma_b,1,5,4	script	#BakonawaDeadn1	CLEAR_NPC,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#BakonawaDeadn1");
	end;
OnStart:
	enablenpc instance_npcname("#BakonawaDeadn1");
	initnpctimer;
	end;
OnTimer100:
	mapannounce instance_mapname("1@ma_b"),"Taho: Que bom! Bakonawa se escondeu debaixo d'água. Agora, ele tentará engolir a lua.",bc_map,"0x00ffff";
	end;
OnTimer5000:
	mapannounce instance_mapname("1@ma_b"),"Taho: Precisamos fazer barulhos altos com os caldeirões e os gongos para que ele não consiga se concentrar na lua!",bc_map,"0x00ffff";
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"Taho: Não lute com ele mesmo que ele saia para a superfície. Apenas faça barulhos altos!",bc_map,"0x00ffff";
	end;
OnTimer15000:
	mapannounce instance_mapname("1@ma_b"),"Missão - Destrua 2 caldeirões e 2 gongos nos lados esquerdo e direito do lago.",bc_map,"0xff3300";
	donpcevent instance_npcname("#Bakonawan2")+"::OnStart";
	stopnpctimer;
	disablenpc instance_npcname("#BakonawaDeadn1");
	end;
}

1@ma_b,36,111,4	script	#Bakonawan2	CLEAR_NPC,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan2");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan2");
	.@label$ = instance_npcname("#Bakonawan2")+"::OnMyMobDead";
	.@map$ = instance_mapname("1@ma_b");
	monster .@map$,95,98,"Caldeirão",MA_DRUM,1,.@label$;
	monster .@map$,60,98,"Caldeirão",MA_DRUM,1,.@label$;
	monster .@map$,97,104,"Gongo",MA_DRUM,1,.@label$;
	monster .@map$,58,104,"Gongo",MA_DRUM,1,.@label$;
	donpcevent instance_npcname("#Bakonawan2-1")+"::OnStart";
	initnpctimer;
	end;
OnMyMobDead:
	.@map$ = instance_mapname("1@ma_b");
	.@mob_dead_num = mobcount(.@map$,instance_npcname("#Bakonawan2")+"::OnMyMobDead");
	if (.@mob_dead_num < 1) {
		donpcevent instance_npcname("#Bakonawan2-1")+"::OnEnd";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan2");
	} else
		mapannounce .@map$,"Taho: Bom "+.@mob_dead_num+" ainda restante!",bc_map,"0x00ffff";
	 end;
OnTimer1000:	callsub OnAnnounce,"5 minutos",15,1;
OnTimer60000:	callsub OnAnnounce,"4 minutos",15,1;
OnTimer120000:	callsub OnAnnounce,"3 minutos",15,1;
OnTimer180000:	callsub OnAnnounce,"2 minutos",15,1;
OnTimer240000:	callsub OnAnnounce,"1 minuto",15,1;
OnTimer270000:	callsub OnAnnounce,"30 segundos",15,0;
OnTimer280000:	callsub OnAnnounce,"20 segundos",15,0;
OnTimer290000:	callsub OnAnnounce,"10 segundos",15,0;
OnTimer295000:	callsub OnAnnounce,"5 segundos",16,0;
OnTimer296000:	callsub OnAnnounce,"4 segundos",17,0;
OnTimer297000:	callsub OnAnnounce,"3 segundos",18,0;
OnTimer298000:	callsub OnAnnounce,"2 segundos",19,0;
OnTimer299000:	callsub OnAnnounce,"1 segundo",20,0;
OnTimer300000:
	.@map$ = instance_mapname("1@ma_b");
	mapannounce .@map$,"Ele voltou para o lago novamente. Acabou?",bc_map,"0xffff00";
	donpcevent instance_npcname("Taho#Fail")+"::OnStart";
	donpcevent instance_npcname("#Bakonawan2-1")+"::OnFail";
	killmonster .@map$,instance_npcname("#Bakonawan2")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan2");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"Tempo Limite: "+getarg(0),bc_map,"0xff4400";
	if (getarg(2)) donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,36,111,4	script	#Bakonawan2-1	CLEAR_NPC,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan2-1");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan2-1");
	monster instance_mapname("1@ma_b"),78,81,"Bakonawa Depois da Lua",BAKONAWA_2,1,instance_npcname("#Bakonawan2-1")+"::OnMyMobDead"; //original: (78,93)
	end;
OnFail:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
	end;
OnEnd:
	enablenpc instance_npcname("#Bakonawan2-1");
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
	initnpctimer;
	end;
OnTimer1000:
	mapannounce instance_mapname("1@ma_b"),"Taho: Ele voltou para as profundezas do lago. Será que acabamos?",bc_map,"0x00ffff";
	end;
OnTimer5000:
	mapannounce instance_mapname("1@ma_b"),"Taho: Hmm! Algo está se movendo rapidamente embaixo d'água! Ba... Bakonawa está vindo!!",bc_map,"0x00ffff";
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"Missão - Elimine Bakonawa, que ficou ainda mais agressivo.",bc_map,"0xff3300";
	donpcevent instance_npcname("#Bakonawan3")+"::OnStart";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan2-1");
	end;
OnMyMobDead:
	end;
}

1@ma_b,36,111,4	script	#Bakonawan3	CLEAR_NPC,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan3");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan3");
	initnpctimer;
	monster instance_mapname("1@ma_b"),78,81,"Bakonawa Depois da Lua",BAKONAWA_3,1,instance_npcname("#Bakonawan3")+"::OnMyMobDead"; //original: (78,83)
	donpcevent instance_npcname("#Bakonawan3-1")+"::OnStart";
	end;
OnMyMobDead:
	.@map$ = instance_mapname("1@ma_b");
	.@mob_dead_num = mobcount(.@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead");
	if (.@mob_dead_num < 1) {
		mapannounce .@map$,"Taho: Conseguimos! Deu pra ver que ele cuspiu algo que parece uma caixa.",bc_map,"0x00ffff";
		donpcevent instance_npcname("Taho#Completed")+"::OnStart";
		donpcevent instance_npcname("#Bakonawan3-1")+"::OnEnd";
		killmonster .@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan3");
	}
	end;
OnTimer1000:
	callsub OnAnnounce,"10 minutes",15,1;
	end;
OnTimer60000:
OnTimer120000:
OnTimer180000:
OnTimer240000:
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
OnTimer300000:	callsub OnAnnounce,"5 minutos",15,1;
OnTimer360000:	callsub OnAnnounce,"4 minutos",15,1;
OnTimer420000:	callsub OnAnnounce,"3 minutos",15,1;
OnTimer480000:	callsub OnAnnounce,"2 minutos",15,1;
OnTimer540000:	callsub OnAnnounce,"1 minuto",15,1;
OnTimer570000:	callsub OnAnnounce,"30 segundos",15,0;
OnTimer580000:	callsub OnAnnounce,"20 segundos",15,0;
OnTimer590000:	callsub OnAnnounce,"10 segundos",15,0;
OnTimer595000:	callsub OnAnnounce,"5 segundos",16,0;
OnTimer596000:	callsub OnAnnounce,"4 segundos",17,0;
OnTimer597000:	callsub OnAnnounce,"3 segundos",18,0;
OnTimer598000:	callsub OnAnnounce,"2 segundos",19,0;
OnTimer599000:	callsub OnAnnounce,"1 segundo",20,0;
OnTimer600000:
	.@map$ = instance_mapname("1@ma_b");
	mapannounce .@map$,"Bakonawa acabou escapando para o fundo do lago.",bc_map,"0xffff00";
	donpcevent instance_npcname("Taho#Fail")+"::OnStart";
	killmonster .@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan3");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"Tempo Limite: "+getarg(0),bc_map,"0xff4400";
	if (getarg(2)) donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,36,111,4	script	#Bakonawan3-1	CLEAR_NPC,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan3-1");
	end;
OnStart:
	initnpctimer;
	end;
OnEnd:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	//showdigit 0;
	stopnpctimer;
	end;
OnMyMobDead:
	showdigit mobcount(instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead");
	end;
OnTimer120000:	callsub OnMobSpawn,10;
OnTimer180000:	callsub OnMobSpawn,15;
OnTimer240000:	callsub OnMobSpawn,20;
//OnTimer300000:	callsub OnMobSpawn,25;
OnTimer300000:	callsub OnMobSpawn,30;
OnTimer360000:	callsub OnMobSpawn,35;
OnTimer420000:	callsub OnMobSpawn,40;
OnTimer480000:	callsub OnMobSpawn,45;
OnTimer540000:	callsub OnMobSpawn,50;
OnTimer600000:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	stopnpctimer;
	end;
OnMobSpawn:
	.@label$ = instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	.@map$ = instance_mapname("1@ma_b");
	killmonster .@map$,.@label$;
	.@mob_dead_num = mobcount(.@map$,.@label$);
	if (.@mob_dead_num < 50) {
		.@mob_rg = 50 - .@mob_dead_num;
		if (.@mob_rg > getarg(0))
			.@mob_rg = getarg(0);
		while(1) {
			areamonster .@map$,74,74,82,74,"Fantoche de Bakonawa",G_WAKWAK,1,.@label$;
			.@mob_dead_num = mobcount(.@map$,.@label$);
			if (.@mob_dead_num >= .@mob_rg) {
				break;
			}
		}
	}
	end;
}

1@ma_b,62,52,4	script	Taho#Completed	4_M_DEWZATIMAN,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "Parece que você está carregando muitos itens. Volte quando conseguir se desfazer de alguns.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "Você está carregando muitos itens pesados. Retorne novamente quando reduzir o peso.";
		close;
              }
	if (questprogress(12279,HUNTING) == 2) {
		erasequest 12279;
		mes "[Taho]";
		mes "Isso foi encontrado junto ao cadáver do Bakonawa. Seria melhor você pegar para si.";
//		if (IsPremiumPcCafe == 10)
			getitem 6499,7; //Rancor Antigo
//		else
//			getitem 6499,5; //Rancor Antigo
		getexp 0,100000;
		next;
	}
	mes "[Taho]";
	mes "É melhor nos apressar e sair deste lugar o quanto antes. Aqui ainda é um muito perioso mesmo sem a presença de Bakonawa.";
	next;
	mes "[Taho]";
	mes "Vamos usar a corda para subir!";
	close2;
	warp "ma_scene01",175,176;
	end;
OnInstanceInit:
	disablenpc instance_npcname("Taho#Completed");
	end;
OnStart:
	enablenpc instance_npcname("Taho#Completed");
	initnpctimer;
	end;
OnTimer1000:
	monster instance_mapname("1@ma_b"),78,74,"Tesouro de Bakonawa",MA_TREASURE,1;
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"Taho: Após abrir a caixa do tesouro, me visite no topo da colina. Tenho algo para lhe dar.",bc_map,"0x00ffff";
	stopnpctimer;
	end;
}

1@ma_b,62,52,4	script	Taho#Fail	4_M_DEWZATIMAN,{
	mes "[Taho]";
	mes "Hmm. Nós quase conseguimos, mas ele é uma criatura muito feroz...";
	next;
	if (getcharid(0) == getpartyleader(getcharid(1),2)) {
		mes "[Taho]";
		mes "Vamos tentar novamente?";
		next;
		switch(select("Espere um momento!:Claro! Temos que enfrentá-lo novamente!")) {
		case 1:
			mes "[Taho]";
			mes "Aww... quase o tirei daqui.";
			close;
		case 2:
			mes "[Taho]";
			mes "Eu vou utilizar novamente Albopal no lago.";
			next;
			mes "[Taho]";
			mes "Estamos prestes a ver o Bakonawa mais enfurecido do que nunca.";
			disablenpc instance_npcname("Taho#Fail");
			donpcevent instance_npcname("#Bakonawan1")+"::OnStart";
			close;
		}
		close;
	} else {
		mes "[Taho]";
		mes "Estou utilizando a mesma dosagem de Albopal como da última vez. Espero que dê certo.";
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname("Taho#Fail");
	end;
OnStart:
	enablenpc instance_npcname("Taho#Fail");
	end;
}
