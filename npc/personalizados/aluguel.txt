//===== Hercules Script ======================================
//= Universal Rental NPC
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.2
//===== Description: =========================================
//= One-click automatic mount rentals.
//= Replaced 'close' with 'end' [Streusel]
//= Fixed 'close' issues. [Joseph]
//============================================================

prontera,124,201,1	script	Garota Negociante	4_F_JOB_BLACKSMITH,{
	/* FAZER: Isso pode ser otimizado com apenas o setmount(), já que ele pode
Auto-detectar a necessidade adequada de cada classe/habilidade. */
	if (hascashmount()) {
		message strcharinfo(0),"Primeiramente você deve remover a sua montaria.";
		end;
	} else if ((eaclass()&EAJ_THIRDMASK) == EAJ_RANGER && !countitem(Wolf's_Flute)) {
		if (!checkfalcon() && getskilllv(HT_FALCON) && !checkwug()) {
			if (select(" ~ Falcão: ~ Worg") == 1)
				setfalcon;
			else
				getitem Wolf's_Flute, 1;
			specialeffect2 EF_TEIHIT3;
			close;
		} else {
			getitem Wolf's_Flute,1;
		}
	} else if ((eaclass()&EAJ_THIRDMASK) == EAJ_MECHANIC && !checkcart() && getskilllv(MC_PUSHCART)) {
		if (!checkmount() != MOUNT_MADO && getskilllv(NC_MADOLICENCE)) {
			if (select(" ~ Carrinho: ~ M.E.C.H.A") == 1) {
				setcart;
			} else {
				setmount(MOUNT_MADO);
			}
			specialeffect2 EF_TEIHIT3;
			close;
		} else {
			setcart;
		}
	} else if (!checkcart() && getskilllv(MC_PUSHCART)) {
		setcart;
	} else if (!checkfalcon() && getskilllv(HT_FALCON) && !checkwug()) {
		setfalcon;
	} else if (checkmount() != MOUNT_PECO && getskilllv(KN_RIDING)) {
		setmount; // Peco ou Dragão
	} else if (checkmount() != MOUNT_MADO && getskilllv(NC_MADOLICENCE)) {
		setmount(MOUNT_MADO);
	} else {
		message strcharinfo(0),"Você não tem o necessário para utilizar meus serviços.";
		end;
	}
	specialeffect2 EF_TEIHIT3;
	end;
}

//============================================================
// Old changelog
//============================================================
//= A Free PecoPeco and Falcon Breeder
//= 1.1 FIXED checkriding/cart/falcon funcs [Lupus]
//= 1.2 Added Rebirth/Advanced Class support [Mass Zero]
//= 1.3 Simplified the checks of job [Silentdragon]
//= 1.3a Fixed a Typo Error. [Samuray22]
//=      Thanks to ~AnnieRuru~.
//= 1.4 Optimized Script. No need for 50 million Labels. [Spre]
//============================================================